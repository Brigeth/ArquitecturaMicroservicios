plugins {
    id 'jacoco'
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.10.0'
}

def packageName = "com.brigeth"

springBoot {
    mainClass = "${packageName}.ApiCustomerApplication"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //OPENAPI
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.25'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.7.0'
    //lombok
	compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //Mapper
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    //Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'

    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'
    //DOCKER
    implementation 'org.postgresql:postgresql:42.7.3'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/openapi.yaml".toString().replace('\\', '/')
    outputDir = layout.buildDirectory.get().asFile.absolutePath + "/generated"
    apiPackage = "com.brigeth.customer.infrastructure.adapter.input.rest"
    modelPackage = "com.brigeth.customer.infrastructure.adapter.input.rest.model"

    configOptions = [
            interfaceOnly: "true",
            reactive: "true",
            useSpringBoot3: "true",
            useTags: "true",
            skipDefaultInterface: "true",
            useJakartaEe: "true"
    ]
}
sourceSets.main.java.srcDir layout.buildDirectory.dir("generated/src/main/java")

compileJava.dependsOn tasks.openApiGenerate

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
            '**/customer/infrastructure/adapter/input/rest/model/**',
            '**/customer/infrastructure/adapter/input/rest/*Api.class',
            '**/ApiCustomerApplication.class',
            '**/entity/**',
            '**/enums/**',
            '**/exception/**'
        ])
    }))
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
            '**/customer/infrastructure/adapter/input/rest/model/**',
            '**/customer/infrastructure/adapter/input/rest/*Api.class',
            '**/ApiCustomerApplication.class',
            '**/entity/**',
            '**/enums/**',
            '**/exception/**'
        ])
    }))
    
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification




